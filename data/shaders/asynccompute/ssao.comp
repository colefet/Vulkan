#version 450

layout (local_size_x = 16, local_size_y = 16) in;
layout (binding = 0, rgba32f) uniform readonly image2D inputPositionDepth;
layout (binding = 1, rgba8) uniform readonly image2D inputNormal;
layout (binding = 2, rgba8) uniform readonly image2D inputNoise;
layout (binding = 5, r8) uniform image2D resultImage;

layout (constant_id = 0) const int SSAO_KERNEL_SIZE = 64;
layout (constant_id = 1) const float SSAO_RADIUS = 0.5;

layout (binding = 3) uniform UBOSSAOKernel
{
	vec4 samples[SSAO_KERNEL_SIZE];
} uboSSAOKernel;

layout (binding = 4) uniform UBO 
{
	mat4 projection;
} ubo;


void main() 
{
	// Get G-Buffer values
	vec3 fragPos = imageLoad(inputPositionDepth, ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y)).rgb;
	vec3 normal = imageLoad(inputNormal, ivec2(gl_GlobalInvocationID.x, gl_GlobalInvocationID.y)).rgb;
	normal=normalize(normal * 2.0 - 1.0);

	// Get a random vector using a noise lookup
	/*ivec2 texDim = textureSize(samplerPositionDepth, 0); 
	ivec2 noiseDim = textureSize(ssaoNoise, 0);
	const vec2 noiseUV = vec2(float(texDim.x)/float(noiseDim.x), float(texDim.y)/(noiseDim.y)) * inUV;  
	vec3 randomVec = texture(ssaoNoise, noiseUV).xyz * 2.0 - 1.0;*/
	ivec2 noise_coord = ivec2(mod(ivec2(gl_GlobalInvocationID.xy),ivec2(4,4)));
	vec3 randomVec = imageLoad(inputNoise, noise_coord).xyz;
	
	// Create TBN matrix
	vec3 tangent = normalize(randomVec - normal * dot(randomVec, normal));
	vec3 bitangent = cross(tangent, normal);
	mat3 TBN = mat3(tangent, bitangent, normal);

	// Calculate occlusion value
	float occlusion = 0.0f;
	// remove banding
	const float bias = 0.01f;
	for(int i = 0; i < SSAO_KERNEL_SIZE; i++)
	{		
		vec3 samplePos = TBN * uboSSAOKernel.samples[i].xyz; 
		samplePos = fragPos + samplePos * SSAO_RADIUS; 
		
		// project
		vec4 offset = vec4(samplePos, 1.0f);
		offset = ubo.projection * offset; 
		offset.xyz /= offset.w; 
		offset.xyz = offset.xyz * 0.5f + 0.5f; 
		
		//float sampleDepth = -texture(samplerPositionDepth, offset.xy).w; 
		ivec2 sample_offset= ivec2 (offset.xy*imageSize(inputPositionDepth));
		float sampleDepth = -imageLoad(inputPositionDepth, sample_offset).w;

#define RANGE_CHECK 1
#ifdef RANGE_CHECK
		// Range check
		float rangeCheck = smoothstep(0.0f, 1.0f, SSAO_RADIUS / abs(fragPos.z - sampleDepth));
		occlusion += (sampleDepth >= samplePos.z + bias ? 1.0f : 0.0f) * rangeCheck;           
#else
		occlusion += (sampleDepth >= samplePos.z + bias ? 1.0f : 0.0f);  
#endif
	}
	occlusion = 1.0 - (occlusion / float(SSAO_KERNEL_SIZE));
	
	imageStore(resultImage, ivec2(gl_GlobalInvocationID.xy), vec4(occlusion));
}

